start : (struct | definition | function)*

struct : "struct" CNAME _struct_types
_struct_types : _type ("," _type)*

definition : "define" CNAME _type "=" _operand

function : "func" CNAME _function_params statements
_function_params : (locals "," args) | (args "," locals)
locals : "locals" "=" INT
args : "args" "=" INT

statements : _statement+
_statement : instruction | label

instruction : (_nullary_instruction _type?)
            | (_unary_instruction _type _operand)
_nullary_instruction : (HALT | NOOP
                | ADD | SUB | MUL | DIV | MOD
                | POP
                | JMP | JMPT | JMPF
                | TESTEQ | TESTNE | TESTLT | TESTGT
                | CALLVIRT | RET)
_unary_instruction : (LDCONST | LDLOCAL | STLOCAL
              | CALLFUNC
              | NEWSTRUCT | LDFIELD | STFIELD
              | NEWARRAY | LDELEM | STELEM)

label: CNAME ":"

_operand : _number_literal | string_literal | binding

_type : _int | _float | _str | _addr | _void
_int : I8 | I16 | I32 | I64 | U8 | U16 | U32 | U64
_float : F32 | F64
_str : STR
_addr : ADDR
_void : VOID

_number_literal : SIGN? (_int_literal | float_literal)
_int_literal : dec_integer | hex_integer | bin_integer
dec_integer : INT
hex_integer : "0x" HEXADECIMAL
bin_integer : "0b" BINARY+
float_literal : DECIMAL
string_literal : ESCAPED_STRING

binding : CNAME

SIGN : "+" | "-"
HEXADECIMAL : HEXDIGIT+
BINDIGIT : "0" | "1"
BINARY : BINDIGIT+

HALT : "halt"
NOOP : "noop"
ADD : "add"
SUB : "sub"
MUL : "mul"
DIV : "div"
MOD : "mod"
LDCONST : "ldconst"
LDLOCAL : "ldlocal"
STLOCAL : "stlocal"
POP : "pop"
TESTEQ : "testeq"
TESTNE : "testne"
TESTLT : "testlt"
TESTGT : "testgt"
JMP : "jmp"
JMPT : "jmpt"
JMPF : "jmpf"
CALLFUNC : "callfunc"
CALLVIRT : "callvirt"
RET : "ret"
NEWSTRUCT : "newstruct"
LDFIELD : "ldfield"
STFIELD : "stfield"
NEWARRAY : "newarray"
LDELEM : "ldelem"
STELEM : "stelem"

I8 : "i8"
I16 : "i16"
I32 : "i32"
I64 : "i64"
U8 : "u8"
U16 : "u16"
U32 : "u32"
U64 : "u64"
F32 : "f32"
F64 : "f64"
ADDR : "addr"
STR : "str"
VOID : "void"

%import common.ESCAPED_STRING
%import common.INT
%import common.HEXDIGIT
%import common.DECIMAL
%import common.CNAME
%import common.WS
%ignore WS